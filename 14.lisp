(in-package #:cl-user)

(defpackage #:aoc-2018-14
  (:use #:cl
        #:alexandria
        #:aoc-2018
        #:arrows))

(in-package #:aoc-2018-14)

(defparameter *n* "939601")

(defun aoc14a (&optional (n (parse-integer *n*)))
  (loop :with recipes := (make-init-array (+ n 10)
                                          :scores '(3 7))
        :for e0 := 0 :then (mod (+ e0 (aref recipes e0) 1)
                                (fill-pointer recipes))
        :for e1 := 1 :then (mod (+ e1 (aref recipes e1) 1)
                                (fill-pointer recipes))
        :for next-recipes := (combine (aref recipes e0)
                                      (aref recipes e1))
        :do (dolist (r next-recipes)
              (unless (vector-push r recipes)
                (return-from aoc14a
                  (format nil "狺ㄣ镥蜚篚怏羼蝈汩疱瞟ъ轶舂┅┅┅ㄤ彐躅磲脲轭轸狎蜥箝脲徜牾篝徕戾筱矧弩戾è狎蜥磲脲狎蜥箝哄戾礤铘豉疱Ж盹卑烘殪飙痫轭翦横潢躞翎忪徜牾篝徕戾┅ㄤ镬轶筱矧弩狎蜥鲥泗矧瘐箬狎蜥┅┅ㄤ彐躅泔礅轭ㄡ猢戾èǐ猢┅ㄩ弪镳悌Ж癌箴扉舡溟玳趔悌┅ㄤ彐躅箴扉舡溟玳趔ㄣ蝈鲥蝮祜镳烘矧洎航眭祠轲戾鲠祯瀛扉篝ㄦ祜矧卑┅呼桢眭祠轲戾鲠祯瀛扉篝ㄦ祜矧卑┅瑚栝戾矧痨躞瞟痨躞洎恒镬戾泗洎┅ㄤ彐躅犸惚粹é镳糸镱犰瞠┅祜镳瑚轸蝈汩疱航磲脲轭轸狎蜥疳蝮瀛轭翦珏瞟横潢躞翎忪后泔蝈Ж珐瑚轸翎蜱弭航磲鲥泗矧＇溟玳舡汨狎瞟瑚轸翎蜱弭戾铉翳航戾铉翳翎蜱弭烘矧灏航呼桢盹ǐ灏ㄡ蝈蝈汩疱灏暴ㄦ殪飙痫轭翦蝈汩疱螬烘矧灞航呼桢盹ǐ灞ㄡ蝈蝈汩疱灞暴ㄦ殪飙痫轭翦蝈汩疱螬烘矧铄舡蝈汩疱航ㄣ镯忾铄ㄡ蝈蝈汩疱灏ㄡ蝈蝈汩疱灞┅轰ㄤ镬轶铄舡蝈汩疱螬鲥泗矧瘐箬屮翦钿蝈汩疱螬麒孱ㄡ钿窘ㄦ殪飙痫轭翦蝈汩疱螬翎蜱弭戾铉翳祜镳烘矧轰秣铈蝻ūㄦ殪飙痫轭翦蝈汩疱螬烘矧轰秣铈蝻ū翎蜱弭戾铉翳呼横祺狴ㄡ蝈蝈汩疱椹ㄡ蝈翎蜱弭戛┅蝈趱蝾骝镯犸惚粹ōㄦ殪飙痫轭翦蝈汩疱螬翎蜱弭戾铉翳┅┅┅